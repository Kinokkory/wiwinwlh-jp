# モナド

## 目次

* [モナド菩提への八正道](#eightfold-path-to-monad-satori)
* [モナド神話](#monadic-myths)
* [法則](#laws)
* [do 記法](#do-notation)
* [Maybe](#maybe)
* [リスト](#list)
* [IO](#io)
* [要は？](#whats-the-point)
* [読み取りモナド](#reader-monad)
* [書き留めモナド](#writer-monad)
* [状態モナド](#state-monad)
* [モナド・チュートリアル](#monad-tutorials)

## <a name="eightfold-path-to-monad-satori">モナド菩提への八正道</a>

モナドの神秘性とされているものについて、感傷的に、抒情的になった人々が、多くのインクを費やしてきました。私はその代わりに、啓蒙への道筋を提案します。

1. モナドのチュートリアルを読むな。
2. いや本当に、モナドのチュートリアルを読むな。
3. Haskell の型について学べ。
4. 型クラスとは何なのかを学べ。
5. [Typeclassopedia](http://www.haskell.org/haskellwiki/Typeclassopedia) を読め。
6. モナドの定義を読め。
7. モナドを実際のコードで使え。
8. モナドのアナロジーのチュートリアルを書くな。

言い換えると、モナドを理解する唯一の道は、良質なソースを読み、GHC を立ち上げてコードを書くことです。アナロジーやメタファーは理解につながりません。

## <a name="monadic-myths">モナド神話</a>

以下のことは全て**誤り**です。

* モナドは純粋でない。
* モナドは副作用に関するものである。
* モナドは状態に関するものである。
* モナドは命令型の順序付けに関するものである。
* モナドは IO に関するものである。
* モナドは遅延性に依存している。
* モナドは副作用を実行するために言語に付けられた”裏口”である。
* モナドは Haskell に埋め込まれた命令型言語である。
* モナドを使うには抽象的な数学を知っていなくてはならない。

参照：

* [What a Monad Is Not](http://www.haskell.org/haskellwiki/What_a_Monad_is_not)

## <a name="laws">法則</a>

Monads are not complicated, the implementation is a typeclass with two
functions, ``(>>=)`` pronounced "bind" and ``return``. Any preconceptions one
might have for the word "return" should be discarded, it has an entirely
different meaning.

```haskell
class Monad m where
  (>>=)  :: m a -> (a -> m b) -> m b
  return :: a -> m a
```
Together with three laws that all monad instances must satisfy.

### 法則 1

```haskell
return a >>= f ≡ f a
```

### 法則 2

```haskell
m >>= return ≡ m
```

### 法則 3

```haskell
(m >>= f) >>= g ≡ m >>= (\x -> f x >>= g)
```

There is an auxiliary function (``(>>)``) defined in terms of the bind operation
that discards its argument.

```haskell
(>>) :: Monad m => m a -> m b -> m b
m >> k = m >>= \_ -> k
```

See: [Monad Laws](http://www.haskell.org/haskellwiki/Monad_laws)

## <a name="do-notation">do 記法</a>

Monads syntax in Haskell is written in sugared form that is entirely equivalent
to just applications of the monad operations. The desugaring is defined
recursively by the rules:

```haskell
do { a <- f ; m } ≡ f >>= \a -> do { m }
do { f ; m } ≡ f >> do { m }
do { m } ≡ m
```

So for example the following are equivalent:

```haskell
do
  a <- f
  b <- g
  c <- h
  return (a, b, c)

do {
  a <- f ;
  b <- g ;
  c <- h ;
  return (a, b, c)
}

f >>= \a ->
  g >>= \b ->
    h >>= \c ->
      return (a, b, c)
```

If one were to write the bind operator as an uncurried function ( this is
not how Haskell uses it ) the same desugaring might look something like the
following chain of nested binds with lambdas.

```haskell
bindMonad(f, lambda a:
  bindMonad(g, lambda b:
    bindMonad(h, lambda c:
      returnMonad (a,b,c))))
```

In the do-notation the monad laws from above are equivalently written:

### 法則 1

```haskell
  do x <- m
     return x

= do m
```

### 法則 2

```haskell
  do y <- return x
     f y

= do f x
```

### 法則 3

```haskell
  do b <- do a <- m
             f a
     g b

= do a <- m
     b <- f a
     g b

= do a <- m
     do b <- f a
        g b
```

See: [Haskell 2010: Do Expressions](http://www.haskell.org/onlinereport/haskell2010/haskellch3.html#x8-470003.14)

## <a name="maybe">Maybe</a>

The *Maybe* monad is the simplest first example of a monad instance. The Maybe
monad models computations which fail to yield a value at any point during
computation.

```haskell
data Maybe a = Just a | Nothing
```

```haskell
instance Monad Maybe where
  (Just x) >>= k = k x
  Nothing  >>= k = Nothing

  return = Just
```

```haskell
(Just 3) >>= (\x -> return (x + 1))
-- Just 4

Nothing >>= (\x -> return (x + 1))
-- Nothing

return 4 :: Maybe Int
-- Just 4
```

```haskell
example1 :: Maybe Int
example1 = do
  a <- Just 3
  b <- Just 4
  return $ a + b
-- Just 7

example2 :: Maybe Int
example2 = do
  a <- Just 3
  b <- Nothing
  return $ a + b
-- Nothing
```

## <a name="list">リスト</a>

The *List* monad is the second simplest example of a monad instance.

```haskell
instance Monad [] where
  m >>= f   =  concat (map f m)
  return x  =  [x]
```

So for example with:

```haskell
m = [1,2,3,4]
f = \x -> [1,0]
```

The evaluation proceeds as follows:

```haskell
m >>= f
==> [1,2,3,4] >>= \x -> [1,0]
==> concat (map (\x -> [1,0]) [1,2,3,4])
==> concat ([[1,0],[1,0],[1,0],[1,0]])
==> [1,0,1,0,1,0,1,0]
```

The list comprehension syntax in Haskell can be implemented in terms of the list
monad.

```haskell
a = [f x y | x <- xs, y <- ys, x == y ]

-- Identical to `a`
b = do
  x <- xs
  y <- ys
  guard $ x == y
  return $ f x y
```

```haskell
example :: [(Int, Int, Int)]
example = do
  a <- [1,2]
  b <- [10,20]
  c <- [100,200]
  return (a,b,c)
-- [(1,10,100),(1,10,200),(1,20,100),(1,20,200),(2,10,100),(2,10,200),(2,20,100),(2,20,200)]
```

## <a name="io">IO</a>

A value of type ``IO a`` is a computation which, when performed, does some I/O
before returning a value of type ``a``. Desugaring the IO monad:

```haskell
main :: IO ()
main = do putStrLn "What is your name: "
          name <- getLine
          putStrLn name
```

```haskell
main :: IO ()
main = putStrLn "What is your name:" >>=
       \_    -> getLine >>=
       \name -> putStrLn name
```

```haskell
main :: IO ()
main = putStrLn "What is your name: " >> (getLine >>= (\name -> putStrLn name))
```

See: [Haskell 2010: Basic/Input Output](http://www.haskell.org/onlinereport/haskell2010/haskellch7.html)

## <a name="whats-the-point">要は？</a>

Consider the non-intuitive fact that we now have a uniform interface for talking
about three very different but foundational ideas for programming: *Failure*,
*Collections*, and *Effects*.

Let's write down a new function called ``sequence`` which folds a function
``mcons``, which we can think of as analogues to the list constructor (i.e. ``(a
: b : [])``) except it pulls the two list elements out of of two monadic values
(``p``,``q``) using bind.

```haskell
sequence :: Monad m => [m a] -> m [a]
sequence = foldr mcons (return [])

mcons :: Monad m => m t -> m [t] -> m [t]
mcons p q = do
  x <- p
  y <- q
  return (x:y)
```

What does this function mean in terms of each of the monads discussed above?

### Maybe

Sequencing a list of a ``Maybe`` values allows us to collect the results of a
series of computations which can possibly fail and yield the aggregated values
only if they all succeeded.

```haskell
sequence :: [Maybe a] -> Maybe [a]
```

```haskell
sequence [Just 3, Just 4]
-- Just [3,4]
sequence [Just 3, Just 4, Nothing]
-- Nothing
```

### リスト

Since the bind operation for the list monad forms the pairwise list of elements
from the two operands, folding the bind over a list of lists with ``sequence``
implements the general Cartesian product for an arbitrary number of lists.

```haskell
sequence :: [[a]] -> [[a]]
```

```haskell
sequence [[1,2,3],[10,20,30]]
-- [[1,10],[1,20],[1,30],[2,10],[2,20],[2,30],[3,10],[3,20],[3,30]]
```

### IO

Sequence takes a list of IO actions, performs them sequentially, and returns the
list of resulting values in the order sequenced.

```haskell
sequence :: [IO a] -> IO [a]
```

```haskell
sequence [getLine, getLine]
-- a
-- b
-- ["a","b"]
```

So there we have it, three fundamental concepts of computation that are normally
defined independently of each other actually all share this similar structure
that can be abstracted out and reused to build higher abstractions that work for
all current and future implementations. If you want a motivating reason for
understanding monads, this is it! This is the essence of what I wish I knew
about monads looking back.

See: [Control.Monad](http://hackage.haskell.org/package/base-4.6.0.1/docs/Control-Monad.html#g:4)

## <a name="reader-monad">読み取りモナド</a>

The reader monad lets us access shared immutable state within a monadic context.

```haskell
ask :: Reader r a
asks :: (r -> a) -> Reader r a
local :: (r -> b) -> Reader b a -> Reader r a
runReader :: Reader r a -> r -> a
```

```haskell
import Control.Monad.Reader

data MyContext = MyContext
  { foo :: String
  , bar :: Int
  } deriving (Show)

computation :: Reader MyContext (Maybe String)
computation = do
  n <- asks bar
  x <- asks foo
  if n > 0
    then return (Just x)
    else return Nothing

ex1 :: Maybe String
ex1 = runReader computation $ MyContext "hello" 1

ex2 :: Maybe String
ex2 = runReader computation $ MyContext "haskell" 0
```

A simple implementation of the Reader monad:

```haskell
newtype Reader r a = Reader { runReader :: r -> a }

instance Monad (Reader r) where
  return a = Reader $ \_ -> a
  m >>= k  = Reader $ \r -> runReader (k (runReader m r)) r

ask :: Reader a a
ask = Reader id

asks :: (r -> a) -> Reader r a
asks f = Reader f

local :: (r -> b) -> Reader b a -> Reader r a
local f m = Reader $ runReader m . f
```

## <a name="writer-monad">書き留めモナド</a>

The writer monad lets us emit a lazy stream of values from within a monadic context.

```haskell
tell :: w -> Writer w ()
execWriter :: Writer w a -> w
runWriter :: Writer w a -> (a, w)
```

```haskell
import Control.Monad.Writer

type MyWriter = Writer [Int] String

example :: MyWriter
example  = do
  tell [1..5]
  tell [5..10]
  return "foo"

output :: (String, [Int])
output = runWriter example
```

A simple implementation of the Writer monad:

```
import Data.Monoid

newtype Writer w a = Writer { runWriter :: (a, w) }

instance Monoid w => Monad (Writer w) where
  return a = Writer (a, mempty)
  m >>= k  = Writer $ let
      (a, w)  = runWriter m
      (b, w') = runWriter (k a)
      in (b, w `mappend` w')

execWriter :: Writer w a -> w
execWriter m = snd (runWriter m)

tell :: w -> Writer w ()
tell w = Writer ((), w)
```

This implementation is lazy so some care must be taken that one actually wants
to only generate a stream of thunks.  Often this it is desirable to produce a
computation which requires a stream of thunks that can pulled lazily out of the
``runWriter``, but often times the requirement is to produce a finite stream of
values that are forced at the invocation of ``runWriter``. Undesired laziness
from Writer is a common source of grief, but is very remediable.

## <a name="state-monad">状態モナド</a>

The state monad allows functions within a stateful monadic context to access and modify shared state.

```haskell
runState  :: State s a -> s -> (a, s)
evalState :: State s a -> s -> a
execState :: State s a -> s -> s
```

```haskell
import Control.Monad.State

test :: State Int Int
test = do
  put 3
  modify (+1)
  get

main :: IO ()
main = print $ execState test 0
```

The state monad is often mistakenly described as being impure, but it is in fact
entirely pure and the same effect could be achieved by explicitly passing state.
A simple implementation of the State monad is only a few lines:

```haskell
newtype State s a = State { runState :: s -> (a,s) }

instance Monad (State s) where
  return a = State $ \s -> (a, s)

  State act >>= k = State $ \s ->
    let (a, s') = act s
    in runState (k a) s'

get :: State s s
get = State $ \s -> (s, s)

put :: s -> State s ()
put s = State $ \_ -> ((), s)

modify :: (s -> s) -> State s ()
modify f = get >>= \x -> put (f x)

evalState :: State s a -> s -> a
evalState act = fst . runState act

execState :: State s a -> s -> s
execState act = snd . runState act
```

## <a name="monad-tutorials">モナド・チュートリアル</a>

So many monad tutorials have been written that it begs the question, what are
monads so difficult when first learning Haskell. I suggest there are three
aspects to why this is so:

1. *There are several levels on indirection with desugaring.*

A lot of Haskell that we write is radically rearranged and transformed into
entirely form under the hood.

Most monad tutorials will not manually expand out the do-sugar. This leaves the
beginner thinking that monads are a way of dropping into a pseudo-imperative
language inside of code and further fuels that misconception that specific
instances like IO are monads in their full generality.

```haskell
main = do
  x <- getLine
  putStrLn x
  return ()
```

Being able to manually desugar is crucial to understanding.

```haskell
main =
  getLine >>= \x ->
    putStrLn x >>= \_ ->
      return ()
```

2. *Asymmetric binary infix operators for higher order functions are not common
   in other languages.*

```haskell
(>>=) :: Monad m => m a -> (a -> m b) -> m b
```

On the left hand side of the operator we have an ``m a`` and on the right we
have ``a -> m b``. Although some languages do have infix operators that are
themselves higher order functions, it is still a rather rate occurrence.

So with a function desugared, it can confusing that ``(>>=)`` operator is in
fact building up a much larger function by composing functions together.

```haskell
main =
  getLine >>= \x ->
    putStrLn >>= \_ ->
      return ()
```

Written in prefix form, it becomes a little bit more digestible.

```haskell
main =
  (>>=) getLine (\x ->
    (>>=) putStrLn (\_ ->
          return ()
    )
  )
```

Perhaps even removing the operator entirely might be more intuitive coming from
other languages.

```haskell
main = bind getLine (\x -> bind putStrLn (\_ -> return ()))
  where
    bind x y = x >>= y
```

3. *Ad-hoc polymorphism is not common place in other languages.*

Haskell's implementation overloading can be unintuitive if not familiar with
type inference. It is abstracted away from the user but the ``(>>=)`` or
``bind`` function is really a function of 3 arguments with the extra typeclass
dictionary argument (``$fMonad``) implicitly threaded around.

```haskell
main $fMonad = bind $dMonad getLine (\x -> bind $dMonad putStrLn (\_ -> return $dMonad ()))
```

Except in the case where the parameter of the monad class is unified ( through
inference ) with a concrete class instance, in which case the instance
dictionary (``$dMonadIO``) is instead spliced throughout.

```haskell
main :: IO ()
main = bind $dMonadIO getLine (\x -> bind $dMonadIO putStrLn (\_ -> return $dMonadIO ()))
```

Now, all of these transformations are trivial once we understand them, they're
just typically not discussed. In my opinion the fundamental fallacy of monad
tutorials is that not intuition for monads is hard to convey ( nor are metaphors
required!), but that novices often come to monads with an incomplete
understanding of points (1), (2), and (3) and then trip on the simple fact that
monads are the first example of a Haskell construct that is the confluence of
all three.

See: [Monad Tutorial Fallacy](http://byorgey.wordpress.com/2009/01/12/abstraction-intuition-and-the-monad-tutorial-fallacy/)